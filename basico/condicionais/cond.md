# üî• cond

O `cond/0` nos ajuda a **condicionar um valor de acordo com diferentes valores de entrada**. Caindo em cada caso especificado pelo valor esperado. Normalmente utilizando [pattern matching](../pattern-matching.md).&#x20;

A defini√ß√£o dele segue a palavra chave `cond` seguido de `do`. Todo o controle de fluxo √© tido dentro dele, exemplo:

{% code lineNumbers="true" %}
```elixir
x = 3 # declara√ß√£o de v√°riavel que ser√° usada no cond

cond do
  x == 5 ->
   "Isso n√£o ser√° verdadeiro"
   
  (x + 8) > 10 ->
    "Nem isso"
    
  x == 3 ->
    "Mas isso ser√°" # logo essa clausura ser√° sucesso e esse bloco ser√° executado.
    
  true ->
    "Nunca vir√° aqui, pois a clausura anterior √© verdadeira"
end
```
{% endcode %}

Vamos a um exemplo mais √∫til. Voc√™ √© um cientista em uma grande empresa farmac√™utico. Temos v√°rios n√≠veis de acesso que v√£o de 1 a 3, sendo 1 o n√≠vel de visitantes e 3 o n√≠vel de acesso a armas biol√≥gicas.&#x20;

Precisamos ent√£o dar permiss√£o a cada √°rea de acordo com seu n√≠vel de acesso.&#x20;

{% hint style="danger" %}
**Lembrando que estamos falando de autoriza√ß√£o e n√£o autentica√ß√£o.**&#x20;
{% endhint %}

Nossos crit√©rios de aceite ser√£o:

* Quando usu√°rio tiver acesso 1, exibir mensagem "_**Bem vindo {nome}, sinta-se**_** √† vontade **_**em nossas estruturas!**_**"**
* Quando usu√°rio tiver acesso 2, mostrar ultima pend√™ncia no setor da sa√∫de junto com o nome do usu√°rio _**"Bem vindo Jo√£o, o Ibuprofeno x42 esta nas fases finais"**_
* Quando o usu√°rio tiver acesso 3, mostrar ultimo status do nemesis. **"Sr. Iago, j√° mandamos nemesis para Raccoon city"**

Vamos come√ßar criando nosso teste:

{% code title="tests/authorization_test.exs" lineNumbers="true" %}
```elixir
defmodule AuthorizationTest do
  use ExUnit.Case
  
  describe "access/1" do
    test "User is a guest" do
      person = %{name: "Paulo", level: 1}
      result = Authorization.access(person)
  
      assert result == "Bem vindo #{person.name}, sinta-se √† vontade em nossas estruturas!"
    end
  end
end
```
{% endcode %}

```shell
mix test test/authorization_test.exs
warning: Authorization.access/1 is undefined (module Authorization is not available or is yet to be defined)
  test/authorization_test.exs:7: AuthorizationTest."test access/1 User is a guest"/1



  1) test access/1 User is a guest (AuthorizationTest)
     test/authorization_test.exs:5
     ** (UndefinedFunctionError) function Authorization.access/1 is undefined (module Authorization is not available)
     code: result = Authorization.access(person)
     stacktrace:
       Authorization.access(%{level: 1, name: "Iago"})
       test/authorization_test.exs:7: (test)


Finished in 0.03 seconds (0.00s async, 0.03s sync)
1 test, 1 failure
```

Recebemos um relat√≥rio dizendo que Authorization.access n√£o est√° dispon√≠vel. Claramente, por que n√£o possu√≠mos esse modulo. Criaremos ele

{% code title="lib/authorization.ex" lineNumbers="true" %}
```elixir
defmodule Authorization do
  def access(_user) do

  end
end
```
{% endcode %}

Criamos o modulo de `Authorization` com uma fun√ß√£o chamada `access/1` que cont√©m um vari√°vel `_user`. Se rodarmos o teste conseguiremos outro erro.

```elixir
mix test test/authorization_test.exs
Compiling 1 file (.ex)
Generated hello_world app


  1) test access/1 User is a guest (AuthorizationTest)
     test/authorization_test.exs:5
     Assertion with == failed
     code:  assert result == "Bem vindo #{person.name}, sinta-se √† vontade em nossas estruturas!"
     left:  nil
     right: "Bem vindo Paulo, sinta-se √† vontade em nossas estruturas!"
     stacktrace:
       test/authorization_test.exs:9: (test)


Finished in 0.02 seconds (0.00s async, 0.02s sync)
1 test, 1 failure
```

Recebemos agora outro erro, dizendo que precisamos de um texto, mas o que recebemos foi um `nil`. Isso por que n√£o estamos retornando nada da fun√ß√£o chamada. Vamos fazer o m√≠nimo para passar esse teste, retornaremos da fun√ß√£o, o texto completo.

<pre class="language-elixir" data-title="lib/authorization.ex" data-line-numbers><code class="lang-elixir">defmodule Authorization do
  def access(_user) do
<strong>    "Bem vindo Paulo, sinta-se √† vontade em nossas estruturas!"
</strong>  end
end
</code></pre>

Se rodarmos agora, os testes ir√£o passar.

```shell
mix test test/authorization_test.exs
.
Finished in 0.02 seconds (0.00s async, 0.02s sync)
1 test, 0 failures
```

Uma vez os testes passando podemos refatorar. O primeiro passo, √© deixa-lo dinamico. Vou adicionar uma nova valida√ß√£o junto no mesmo teste, apenas para comprovar que est√° correto. Vamos alterar nosso teste:

<pre class="language-elixir"><code class="lang-elixir">defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    test "User is a guest" do
<strong>      person1 = %{name: "Paulo", level: 1}
</strong><strong>      person2 = %{name: "Jos√©", level: 1}
</strong>
<strong>      result1 = Authorization.access(person1)
</strong><strong>      result2 = Authorization.access(person2)
</strong>
<strong>      assert result1 == "Bem vindo #{person1.name}, sinta-se √† vontade em nossas estruturas!"
</strong><strong>      assert result2 == "Bem vindo #{person2.name}, sinta-se √† vontade em nossas estruturas!"
</strong>    end
  end
end
</code></pre>

Se rodarmos o teste assim ele acusar√° um erro

```shell
mix test test/authorization_test.exs


  1) test access/1 User is a guest (AuthorizationTest)
     test/authorization_test.exs:5
     Assertion with == failed
     code:  assert result2 == "Bem vindo #{person2.name}, sinta-se √† vontade em nossas estruturas!"
     left:  "Bem vindo Paulo, sinta-se √† vontade em nossas estruturas!"
     right: "Bem vindo Jos√©, sinta-se √† vontade em nossas estruturas!"
     stacktrace:
       test/authorization_test.exs:13: (test)


Finished in 0.02 seconds (0.00s async, 0.02s sync)
1 test, 1 failureshe
```

Isso por que, em nossa implementa√ß√£o n√£o utilizamos a vari√°vel que mandamos por par√¢metro. Logo, ela est√° fixado como Paulo. Vamos arrumar isso.

<pre class="language-elixir" data-title="lib/authorization.ex" data-line-numbers><code class="lang-elixir">defmodule Authorization do
  def access(user) do
<strong>    "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"
</strong>  end
end
</code></pre>

Removemos o underline (\_) de user e utilizamos na interpola√ß√£o da linha 3. Se rodarmos agora esse teste, teremos sucesso.

```shell
mix test test/authorization_test.exs
Compiling 1 file (.ex)
.
Finished in 0.01 seconds (0.00s async, 0.01s sync)
1 test, 0 failures
```

Agora possu√≠mos apenas mais um ponto para ver. O que acontece caso mandamos um map incorreto para dentro da fun√ß√£o `access`? Por exemplo `Authorization.access("lalala")`.

Vamos validar isso no terminal iterativo do elixir. Para isso queremos que esse projeto que estamos mexendo compile junto para que possamos usar o modulo `Authorization`. Para isso, basta executar na pasta raiz de seu projeto `iex -S mix` e o terminal iterativo ser√° aberto:

```shell
iex -S mix
Erlang/OTP 25 [erts-13.1.4] [source] [64-bit] [smp:16:16] [ds:16:16:10] [async-threads:1] [jit:ns]

Interactive Elixir (1.14.3) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)> 
```

Caso n√£o saiba o que √© isso, v√° para a sess√£o [iEX](../../primeiros-passos-em-elixir/iex.md) do livro.&#x20;

Vamos executar a fun√ß√£o Authorization.access/1 utilizando como par√¢metro algo incorreto, como "lalala"

```shell
iex(1)> Authorization.access("lalala")
** (KeyError) key :name not found in: "lalala". If you are using the dot syntax, such as map.field, make sure the left-hand side of the dot is a map
    (hello_world 0.1.0) lib/authorization.ex:3: Authorization.access/1
```

Isso acontece porque na fun√ß√£o `access/1` tentamos acessar um elemento chamado `name` dentro do map `user`. J√° que user agora √© "lalala", uma string, ele n√£o possui esse elemento. Ele nem √© um map, logo, n√£o temos como acessa-lo. Temos v√°rias formas de resolver esse problema.&#x20;

Primeira coisa que iremos fazer √© criar um teste para entender o que precisamos que seja retornado caso seja invalido. Criaremos um teste chamado `invalid_params` e adicionaremos o cen√°rio de falha ali.

{% code title="test/authorization_test.exs" lineNumbers="true" %}
```elixir
defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    test "invalid params" do
      invalid_params = "lalala"
      {:error, reason} = Authorization.access(invalid_params)
      
      assert reason == "invalid_params"
    end
    
    # ...
  end
end
```
{% endcode %}

Criamos um teste simples para garantir o comportamento. Se voc√™ analisar o c√≥digo, ver√° que agora adicionamos a conven√ß√£o de [:ok/:error](../../conceitos/convencoes.md#tupla-ok-result-e-error-reason) para conseguirmos facilmente identificar quando algo da certo e quando algo da errado.

Se rodarmos o teste, vamos ver que conseguimos reproduzir o erro no teste, o que √© um √≥timo sinal.

```shell
mix test test/authorization_test.exs


  1) test access/1 invalid params (AuthorizationTest)
     test/authorization_test.exs:5
     ** (KeyError) key :name not found in: "lalala". If you are using the dot syntax, such as map.field, make sure the left-hand side of the dot is a map
     code: {:error, reason} = Authorization.access(invalid_params)
     stacktrace:
       (hello_world 0.1.0) lib/authorization.ex:3: Authorization.access/1
       test/authorization_test.exs:7: (test)

.
Finished in 0.03 seconds (0.00s async, 0.03s sync)
2 tests, 1 failure
```

Agora precisamos lidar com ele. Para isso, utilizaremos a valida√ß√£o de tipo usando uma estrutura que ja possu√≠mos no projeto. A estrutura `User`. Nela j√° possu√≠mos o `name`.&#x20;

{% hint style="info" %}
Utilizo uma estrutura ja criada para mostrar a reutiliza√ß√£o de m√≥dulos e tamb√©m como seria um fluxo de atualiza√ß√£o do mesmo, chegando mais perto do mundo real.
{% endhint %}

Caso voc√™ n√£o a tenha por algum motivo, vou colocar aqui para facilitar

{% code title="lib/user.ex" lineNumbers="true" %}
```elixir
defmodule User do
  defstruct [:id, :name]
end
```
{% endcode %}

Tendo a estrutura, precisamos garantir que nossa fun√ß√£o `access` aceite receber apenas ela. Vamos atualizar nosso c√≥digo.

{% code title="lib/authorization.ex" lineNumbers="true" %}
```elixir
defmodule Authorization do
  def access(user = %User{}) do
    "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"
  end
end
```
{% endcode %}

Se rodar novamente o erro, teremos um relat√≥rio diferente

```shell
mix test test/authorization_test.exs
Compiling 1 file (.ex)


  1) test access/1 User is a guest (AuthorizationTest)
     test/authorization_test.exs:12
     ** (FunctionClauseError) no function clause matching in Authorization.access/1

     The following arguments were given to Authorization.access/1:
     
         # 1
         %{level: 1, name: "Paulo"}
     
     Attempted function clauses (showing 1 out of 1):
     
         def access(user = %User{})
     
     code: result1 = Authorization.access(person1)
     stacktrace:
       (hello_world 0.1.0) lib/authorization.ex:2: Authorization.access/1
       test/authorization_test.exs:16: (test)



  2) test access/1 invalid params (AuthorizationTest)
     test/authorization_test.exs:5
     ** (FunctionClauseError) no function clause matching in Authorization.access/1

     The following arguments were given to Authorization.access/1:
     
         # 1
         "lalala"
     
     Attempted function clauses (showing 1 out of 1):
     
         def access(user = %User{})
     
     code: {:error, reason} = Authorization.access(invalid_params)
     stacktrace:
       (hello_world 0.1.0) lib/authorization.ex:2: Authorization.access/1
       test/authorization_test.exs:7: (test)


Finished in 0.02 seconds (0.00s async, 0.02s sync)
2 tests, 2 failuresshel
```

Recebemos dois erros, vamos focar no primeiro agora. J√° que nossa fun√ß√£o access permite apenas receber a estrutura `User` agora, precisamos alterar nossos dados de testes para enviar o `User` para `access`.

<pre class="language-elixir" data-title="test/authorization_test.exs" data-line-numbers><code class="lang-elixir">defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    test "User is a guest" do
<strong>      person1 = %User{name: "Paulo", level: 1}
</strong><strong>      person2 = %User{name: "Jos√©", level: 1}
</strong>
      result1 = Authorization.access(person1)
      result2 = Authorization.access(person2)

      assert result1 == "Bem vindo #{person1.name}, sinta-se √† vontade em nossas estruturas!"
      assert result2 == "Bem vindo #{person2.name}, sinta-se √† vontade em nossas estruturas!"
    end
  end
end
</code></pre>

```shell
mix test test/authorization_test.exs

== Compilation error in file test/authorization_test.exs ==
** (KeyError) key :level not found
    (hello_world 0.1.0) expanding struct: User.__struct__/1
    test/authorization_test.exs:13: AuthorizationTest."test access/1 User is a guest"/1
```

Recebemos um outro erro, dizendo que a `key :level not found`. Quando utilizamos structure, precisamos utilizar apenas a estrutura de dados criada nele. Em user n√£o possu√≠mos um campo :level e isso faz com que a compila√ß√£o falhe.&#x20;

{% hint style="danger" %}
A utiliza√ß√£o de estrutura engessa os dados nele, garantindo que a estrutura seja sempre a mesma. Isso √© um bom ganho quando tratamos com dados.
{% endhint %}

Precisamos adicionar `:level` em `User`. √â uma simples mudan√ßa

{% code title="lib/user.ex" lineNumbers="true" %}
```elixir
defmodule User do
  defstruct [:id, :name, level: 1]
end
```
{% endcode %}

Para garantir consist√™ncia e seguran√ßa em nosso projeto, podemos configurar que, caso n√£o passado o `level`, ele ser√° automaticamente 1, que significa que ele √© visitante.

Podemos agora rodar os testes&#x20;

```shell
mix test test/authorization_test.exs


  1) test access/1 invalid params (AuthorizationTest)
     test/authorization_test.exs:5
     ** (FunctionClauseError) no function clause matching in Authorization.access/1

     The following arguments were given to Authorization.access/1:
     
         # 1
         "lalala"
     
     Attempted function clauses (showing 1 out of 1):
     
         def access(user = %User{})
     
     code: {:error, reason} = Authorization.access(invalid_params)
     stacktrace:
       (hello_world 0.1.0) lib/authorization.ex:2: Authorization.access/1
       test/authorization_test.exs:7: (test)

.
Finished in 0.02 seconds (0.00s async, 0.02s sync)
2 tests, 1 failure
```

Resolvemos o primeiro erro. Agora vamos para o proximo tratamento. Esse aqui esta falando que "lalala" n√£o √© uma estrutura User e ele esta certo. Agora precisamos fazer com que o erro seja retornado. J√° possu√≠mos o texto que precisamos no teste criado em `authorization_test.exs`.

Se voc√™ analisar, estamos usando pattern matching no par√¢metro da fun√ß√£o e devido n√£o ter sido correspondido ele n√£o entrou na primeira fun√ß√£o access. Podemos ent√£o criar uma nova fun√ß√£o de mesmo nome, fazer dar matching e ent√£o retornar a mensagem de erro. Vamos l√°



{% code title="lib/authorization.ex" lineNumbers="true" %}
```elixir
defmodule Authorization do
  def access(user = %User{}) do
    "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"
  end

  def access(_user) do
    {:error, "invalid_params"}
  end
end
```
{% endcode %}

Se rodarmos o teste agora, teremos sucesso.

```shell
mix test test/authorization_test.exs
..
Finished in 0.03 seconds (0.00s async, 0.03s sync)
2 tests, 0 failures
```

Tudo finalizado no primeiro caso, por√©m, temos um inconsist√™ncia. Quando temos sucesso, recebemos a `message`, mas quando temos um erro temos uma estrutura diferente `{:error, reason}`. Devemos deixar elas similares e como estamos usando o padr√£o de [tuplas de ok/error](../../conceitos/convencoes.md#tupla-ok-result-e-error-reason), devemos alterar a resposta de sucesso para recebermos `{:ok, result}`. Mudan√ßa simples, apenas alterar primeiro os testes e depois a implementa√ß√£o.

<pre class="language-elixir" data-title="test/authorization_test.exs" data-line-numbers><code class="lang-elixir">defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    # ...
    test "User is a guest" do
      person1 = %User{name: "Paulo", level: 1}
      person2 = %User{name: "Jos√©", level: 1}

<strong>      {:ok, result1} = Authorization.access(person1)
</strong><strong>      {:ok, result2} = Authorization.access(person2)
</strong>
      assert result1 == "Bem vindo #{person1.name}, sinta-se √† vontade em nossas estruturas!"
      assert result2 == "Bem vindo #{person2.name}, sinta-se √† vontade em nossas estruturas!"
    end
  end
end
</code></pre>

<pre class="language-elixir" data-title="lib/authorization.ex" data-line-numbers><code class="lang-elixir">defmodule Authorization do
  def access(user = %User{}) do
<strong>    {:ok, "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"}
</strong>  end

  # ...
end
</code></pre>

Feito isso, so rodar os testes para garantir que esta tudo certo.

```shell
mix test test/authorization_test.exs
Compiling 1 file (.ex)
..
Finished in 0.02 seconds (0.00s async, 0.02s sync)
2 tests, 0 failures
```

{% hint style="danger" %}
**Uma pausa r√°pida**\
\
Sei que n√£o vimos nada sobre `cond/0` at√© ent√£o, mas isso foi um bom exerc√≠cio para pensar em como desenvolver em elixir. Espero que me perdoem por essa volta, mas as vezes o atalho tem mais perigos que a longa jornada.\
\
Seguimos
{% endhint %}

Conseguimos finalizar o primeiro ponto, vamos seguir para o ponto dois.

* ~~Quando usu√°rio tiver acesso 1, exibir mensagem "_**Bem vindo {nome}, sinta-se**_** ****√† vontade**** **_**em nossas estruturas!**_**"**~~
* Quando usu√°rio tiver acesso 2, mostrar ultima pend√™ncia no setor da sa√∫de junto com o nome do usu√°rio _**"Bem vindo Jo√£o, o Ibuprofeno x42 esta nas fases finais"**_
* Quando o usu√°rio tiver acesso 3, mostrar ultimo status do nemesis. **"Sr. Iago, j√° mandamos nemesis para Raccoon city"**

Vamos agora para o n√≠vel 2, a autoriza√ß√£o de um funcion√°rio. Vamos agora criar um teste para lidar com esse cen√°rio.

{% code title="test/authorization_teste.exs" lineNumbers="true" %}
```elixir
defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    # ...
    test "User is an employee" do
      person = %User{name: "Carlos", level: 2}

      {:ok, result} = Authorization.access(person)


      assert result == "Bem vindo #{person.name}, o Ibuprofeno x42 esta nas fases finais"
    end
  end
end
```
{% endcode %}

O teste agora quer garantir que usu√°rios com n√≠vel 2 de acesso, tenham informa√ß√£o sobre seu dia de trabalho, pois n√≠vel dois se da a funcion√°rios. Se rodarmos esse teste, teremos um relat√≥rio de erro.

```shell
mix test test/authorization_test.exs


  1) test access/1 User is an employee (AuthorizationTest)
     test/authorization_test.exs:5
     Assertion with == failed
     code:  assert result == "Bem vindo #{person.name}, o Ibuprofeno x42 esta nas fases finais"
     left:  "Bem vindo Carlos, sinta-se √† vontade em nossas estruturas!"
     right: "Bem vindo Carlos, o Ibuprofeno x42 esta nas fases finais"
     stacktrace:
       test/authorization_test.exs:11: (test)

..
Finished in 0.04 seconds (0.00s async, 0.04s sync)
3 tests, 1 failure
```

A mensagem que esperavamos n√£o foi entregue. Isso devido a fun√ß√£o access so esta implementado para devolver uma mensagem de primeiro n√≠vel. Vamos alterar utilizando cond/0 para tomar a decis√£o de fluxo que queremos.

{% hint style="info" %}
Relembrando que

\
\* nivel 1 -> mensagem para visitantes\
\* nivel 2 -> mensagem de funcionarios base\
\* nivel 3 -> mensagem de postos altos
{% endhint %}

Vamos alterar nosso c√≥digo

{% code title="lib/authorization.ex" lineNumbers="true" %}
```elixir
defmodule Authorization do
  def access(user = %User{}) do
    cond do
      user.level == 1 ->
        {:ok, "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"}

      user.level == 2 ->
        {:ok, "Bem vindo #{user.name}, o Ibuprofeno x42 esta nas fases finais"}
    end
  end

  def access(_user) do
    {:error, "invalid_params"}
  end
end
```
{% endcode %}

Adicionamos a condicional `cond/0` onde ela lida com o `level` de acesso do usu√°rio. Percebe como fica simples adicionar novos desvios de fluxo?&#x20;

```shell
mix test test/authorization_test.exs
Compiling 1 file (.ex)
...
Finished in 0.02 seconds (0.00s async, 0.02s sync)
3 tests, 0 failures
```

Simples assim, fechamos mais um crit√©rio de aceite.\


* ~~Quando usu√°rio tiver acesso 1, exibir mensagem "_**Bem vindo {nome}, sinta-se**_** ****√† vontade**** **_**em nossas estruturas!**_**"**~~
* ~~Quando usu√°rio tiver acesso 2, mostrar ultima pend√™ncia no setor da sa√∫de junto com o nome do usu√°rio _**"Bem vindo Jo√£o, o Ibuprofeno x42 esta nas fases finais"**_~~
* Quando o usu√°rio tiver acesso 3, mostrar ultimo status do nemesis. **"Sr. Iago, j√° mandamos nemesis para Raccoon city"**

{% hint style="info" %}
**Desafio**\
\
Tente adicionar o level 3 para dar matching na frase: **Sr. {nome}, j√° mandamos Nemesis para Raccoon city.**\
****\
****Voc√™ deve:\
****\
****1. Escrever o teste em authorizatin\_test.exs\
2\. Implementar a solu√ß√£o em authorization.ex
{% endhint %}

Espero que tenha feito o desafio, essas a√ß√µes que fazem internalizarmos o conte√∫do. N√£o irei implementar ele. Esse √© o desafio.&#x20;

Existe apenas mais uma coisa a se fazer. Vamos dizer que eu coloque o n√≠vel de acesso 10. O que aconteceria? Vamos abrir novamente nosso terminal iterativo `iex -S mix` e executaremos a chamada da fun√ß√£o usando o level 10.

```shell
iex(1)> Authorization.access(%User{name: "iago", level: 10})
** (CondClauseError) no cond clause evaluated to a truthy value
    (hello_world 0.1.0) lib/authorization.ex:7: Authorization.access/1
```

Recebemos um relat√≥rio de erro. A parte importante √© `no cond clause evaluated to a truthy value`. Nenhuma condi√ß√£o foi saciada no cond, sem op√ß√£o do que fazer, ele quebra. Temos que ter pelo menos uma condi√ß√£o que verdadeira para n√£o causar problemas a nos. Fora que precisamos avisar ao usu√°rio que √© melhor ele se retirar antes que chame a seguran√ßa. Vamos primeiro criar nosso teste.

```elixir
defmodule AuthorizationTest do
  use ExUnit.Case

  describe "access/1" do
    test "You are supposed to not be here" do
      person = %User{name: "Eduardo", level: 10}

      {:error, reason} = Authorization.access(person)

      assert reason == "#{person.name}, voc√™ n√£o deveria estar aqui. Pe√ßo que se retire"
    end

    # ...
  end
end
```

Execute esse c√≥digo e ver√° que conseguimos reproduzir o problema que enfrentamos no terminal iterativo.&#x20;

```shell
mix test test/authorization_test.exs
Compiling 1 file (.ex)
...

  1) test access/1 You are supposed to not be here (AuthorizationTest)
     test/authorization_test.exs:5
     ** (CondClauseError) no cond clause evaluated to a truthy value
     code: {:error, reason} = Authorization.access(person)
     stacktrace:
       (hello_world 0.1.0) lib/authorization.ex:7: Authorization.access/1
       test/authorization_test.exs:8: (test)
```

Precisamos cuidar disso agora, mas seremos defensivos. Ao invez de fazer alguma regra, deixaremos uma clausura true no final de cond, onde, se nenhuma clausura for saciada, a ultima sera e uma mensagem de erro aparecer√°.

<pre class="language-elixir" data-title="lib/authorization.ex" data-line-numbers><code class="lang-elixir">defmodule Authorization do
  def access(user = %User{}) do
    cond do
      user.level == 1 ->
        {:ok, "Bem vindo #{user.name}, sinta-se √† vontade em nossas estruturas!"}

      user.level == 2 ->
        {:ok, "Bem vindo #{user.name}, o Ibuprofeno x42 esta nas fases finais"}

<strong>      true ->
</strong><strong>        {:error, "#{user.name}, voc√™ n√£o deveria estar aqui. Pe√ßo que se retire"}
</strong>    end
  end

  def access(_user) do
    {:error, "invalid_params"}
  end
end
</code></pre>

Adiconamos uma condi√ß√£o que ser√° sempre `true`, caso nenhuma condi√ß√£o acima seja `true`, ele caira nessa √∫ltima e a mensagem de erro vai ser gerada. Uma solu√ß√£o simples, que funciona bem para n√≥s aqui.&#x20;

### Conclus√£o

Nesse cap√≠tulo, seguimos um fluxo de desenvolvimento comum em elixir, onde criar e refatoramos uma implementa√ß√£o. Tamb√©m entendemos a utilidade da condicional `cond/0` e fizemos um desafio. Lembrem-se, sempre existe outras formas de atender a uma expectativa, podendo muitas vezes, utilizar os mesmos recursos. Tente e re-tente criar de formas diferentes para conseguir extrair os melhores resultados.&#x20;
